;インストールしたAlpine LinuxにSSHログインができるように設定・Docker・webmin・Portainerのインストールを行う

;環境変数を展開して設定を反映
expandenv host "localhost:%QEMU_SERIAL_PORT% /nossh /T=1"
expandenv setupscript "%SCRIPTDIR%\setup.sh"
expandenv loginprompt "%LINUX_SETUP_HOSTNAME% login:"
expandenv prompt "%LINUX_SETUP_HOSTNAME%:"

;念のため3秒待つ
pause 3

;接続
connect host

;ログインプロンプトの表示を待つ
wait loginprompt

;rootでログイン
sendln "root"

wait prompt

;セットアップ用のシェルスクリプトをローカルから読み込んでcatコマンドでリモートに書き込み
fileopen fh setupscript 0

sendln "cat << EOF > setup.sh"

while 1
    filereadln fh buf
    if result == 1 then
        break
    endif
    expandenv exbuf buf
    sendln exbuf
endwhile

sendln "EOF"

fileclose fh

wait prompt

sendln "sh ~/setup.sh"

wait prompt

sendln "rm ~/setup.sh"

wait prompt

;セットアップが終わったのでシャットダウン
sendln "poweroff"

;Telnet切断 = Qemu終了まで待つ
waitevent 4